import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories { mavenCentral() }
    dependencies { classpath 'com.github.abrarsyed.gmcp:GMCP:0.7.3' }
}

apply plugin: 'gmcp'

group = 'net.lepko.easycrafting'
version = '1.2.0'
archivesBaseName = 'EasyCrafting'
minecraft.minecraftVersion = '1.6.2'

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
def filteredSourceDir = "${buildDir}/filteredsrc";

sourceSets {
    filtered {
        java {
            srcDirs(filteredSourceDir)
        }
    }
}

task filterSources (type: Copy) {
    from sourceSets.main.java
    into filteredSourceDir
    filter(ReplaceTokens, tokens: [MOD_VERSION: version, MC_VERSION: minecraft.minecraftVersion])
}

compileJava.source = sourceSets.filtered.java
compileJava.dependsOn filterSources
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

processResources {
    exclude '**/*.psd'

    eachFile { file -> if (!file.getPath().endsWith('.png'))
        filter(ReplaceTokens, tokens: [MOD_VERSION: version, MC_VERSION: minecraft.minecraftVersion])
    }
}

jar {
    appendix = 'universal-' +minecraft.minecraftVersion
}

task devJar (type: Jar) {
    from sourceSets.main.output
    appendix = 'developer-' + minecraft.minecraftVersion
}

artifacts {
    archives devJar
}


